Use Windows.pkg
Use cJsonObject.pkg
Use JSONHelper\cJsonHttpBinaryTransfer.pkg
Use JSONHelper\cWSJson.pkg
Use APIStructs\FedExAPICommon.pkg

Use APIStructs\FedExValidateAddress\stFedExAddressRequest.pkg
Use APIStructs\FedExValidateAddress\stFedExAddressResponse.pkg

Class cFedExAddressValidationAPI is a cFedExAPI
    Procedure Construct_Object
        Forward Send Construct_Object

        Set psLogFileName to "FedExAddressValidation"

        Property stFedExAddressRequest ptFedExAddressRequest
        Property stFedExAddressResponse ptFedExAddressResponse
    End_Procedure

    Function ValidateAddress stFedExAddressRequest tAddressRequest String sClientID String sClientPass String sAccontNum Returns Boolean
        Boolean bOK
        stFedExAddressResponse tResponse

        Set ptFedExAddressRequest  to tAddressRequest //Save this for later use in FillJSONRequest.

        //Save these for use in getting the FedEx API token if the current one has expired.
        Set psClientID      to sClientID
        Set psClientPass    to sClientPass
        Set psAccountNum    to sAccontNum

#IFDEF Is$WebApp
#ELSE
        Send Cursor_Wait            to Cursor_Control
#ENDIF        

        Get PostJSON    to bOK
        
#IFDEF Is$WebApp
#ELSE
        Send Cursor_Ready   to Cursor_Control
#ENDIF        

        Function_Return bOK
    End_Function

    Function SetupPathAndParameters Returns String
        String sURL sParameters

        Move "/address/v1/addresses/resolve"    to sURL

        Function_Return sURL
    End_Function

    Function FillJSONRequest Returns Handle
        Handle hJSON
        stFedExAddressRequest tRequest

        Forward Get FillJSONRequest to hJSON

        Get ptFedExAddressRequest   to tRequest
        Send DataTypeToJson of hJSON tRequest

//        If (pbRemoveNullMembers(Self)) Begin
//            Set pbRequireAllMembers of hJSON    to False
//            Send RemoveEmptyMembers hJSON True True True
//        End
//
//        //It will only do this when you're debugging and skip the If and go in manually...
//        If False Begin
//            String  sTemp
//            Get Stringify   of hJSON    to sTemp
//
//            Boolean bValid
//            Get ParseString of hJSON sTemp  to bValid
//        End

        Function_Return hJSON
    End_Function

    Function ParseJSONResponse Handle hJSONResponse Returns Boolean
        Boolean bOK
        stFedExAddressResponse tResponse
        String  sErr

//        //It will only do this when you're debugging and skip the If and go in manually...
//        If False Begin
//            String  sTemp
//            Get Stringify   of hJSONResponse    to sTemp
//        End

        Set pbRequireAllMembers of hJSONResponse    to False
        Get JsonToDataType      of hJSONResponse    to tResponse
        Get psParseError        of hJSONResponse    to sErr

//ToDo MRM I don't think psParseError is ever going to be set here from JsonToDataType...
        If (Trim(sErr) > '') Begin
            Move False  to bOK
            Send LogError "" "" sErr
            Set psLastError to sErr
        End
        Else Begin
            Move True   to bOK

            Set ptFedExAddressResponse  to tResponse
        End

        Function_Return bOK
    End_Function

End_Class
